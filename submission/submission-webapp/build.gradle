import static Dependencies.MySql
import static SpringBootDependencies.*
import static TestDependencies.*

plugins {
    id "org.jetbrains.kotlin.jvm"
    id "org.jetbrains.kotlin.plugin.spring" version "1.3.20"
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
    id "org.springframework.boot" version "2.1.2.RELEASE"
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile(project(":submission:persistence"))
    compile(project(":submission:submitter"))
    compile(project(":submission:submission-security"))
    compile(project(":commons:commons-serialization"))
    compile(project(":commons:commons-util"))
    compile(project(":commons:commons-test"))
    compile(project(":commons:commons-http"))

    compile(SpringBootStarterWeb)
    compile(SpringBootStarterDataJpa)
    compile(SpringBootStarterConfigProcessor)
    compile(SpringBootStarterSecurity)

    compile(MySql)

    testCompile(project(":bio-webclient"))

    // Junit dependencies
    testCompile(Junit5Api)
    testCompile(Junit5Params)
    runtime(Junit5Engine)
    runtime(Junit5Console)
    testCompile(JunitExtensions)

    testCompile(MockK)
    testCompile(AssertJ)
    testCompile(H2)
    testCompile(KotlinXmlBuilder)
    testCompile(SpringBootStarterTest)
}

sourceSets {
    itest {
        kotlin {
            compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
            runtimeClasspath += output + compileClasspath
        }
    }
}

configurations {
    itestCompile.extendsFrom testCompile
    itestRuntime.extendsFrom testRuntime
}

task itest(type: Test, dependsOn: []) {
    useJUnitPlatform()
    testClassesDirs = sourceSets.itest.output.classesDirs
    classpath = sourceSets.itest.runtimeClasspath
}

test.finalizedBy itest
