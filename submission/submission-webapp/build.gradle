import static Dependencies.MySql
import static SpringBootDependencies.*
import static TestDependencies.*

plugins {
    id "org.jetbrains.kotlin.plugin.spring" version "1.3.20"
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
    id "org.springframework.boot" version "2.1.2.RELEASE"
}

dependencies {
    compile(project(":submission:persistence"))
    compile(project(":submission:submitter"))
    compile(project(":submission:submission-security"))
    compile(project(":submission:notifications"))
    compile(project(":commons:commons-serialization"))
    compile(project(":commons:commons-util"))
    compile(project(":commons:commons-test"))
    compile(project(":commons:commons-http"))

    compile(SpringBootStarterWeb)
    compile(SpringBootStarterDataJpa)
    compile(SpringBootStarterConfigProcessor)
    compile(SpringBootStarterSecurity)

    compile(MySql)

    testCompile(project(":client:bio-webclient"))

    testCompile(BaseTestCompileDependencies)
    testRuntime(BaseTestRuntimeDependencies)
    testCompile(SpringBootStarterTest)
    testCompile(H2)
    testCompile(KotlinXmlBuilder)
}

sourceSets {
    itest {
        kotlin {
            compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
            runtimeClasspath += output + compileClasspath
        }
    }
}

configurations {
    itestCompile.extendsFrom testCompile
    itestRuntime.extendsFrom testRuntime
}

task itest(type: Test, dependsOn: []) {
    useJUnitPlatform()
    testClassesDirs = sourceSets.itest.output.classesDirs
    classpath = sourceSets.itest.runtimeClasspath
}

test.finalizedBy itest

bootJar {
    archiveName = 'submission-webapp.jar'
}
