remotes {
    dev {
        host = 'biostudy-bia.ebi.ac.uk'
        user = 'ma-svc'
    }

    beta {
        host = 'biostudy-dev.ebi.ac.uk'
        user = 'ma-svc'
    }

    prod {
        host = 'biostudy-prod.ebi.ac.uk'
        user = 'ma-svc'
    }
}

ssh.settings {
    knownHosts = allowAnyHosts
    identity = System.getenv("MASVC_SSH_KEY")
    logging = "stdout"
}

task deploySubmitter {
    doFirst {
        project.ext.artifactPath = "submission/submission-webapp/build/libs"
        project.ext.artifactName = "submission-webapp-1.0.0.jar"
        project.ext.appPort = 8788
        project.ext.debugPort = 8003
    }

    finalizedBy "deploy"
}

task deployHandlers {
    doFirst {
        project.ext.artifactPath = "submission/submission-handlers/build/libs"
        project.ext.artifactName = "submission-handlers-1.0.0.jar"
        project.ext.appPort = 8392
        project.ext.debugPort = 8504
    }

    finalizedBy "deploy"
}


task deployScheduler {
    doFirst {
        project.ext.artifactPath = "scheduler/scheduler/build/libs"
        project.ext.artifactName = "scheduler-1.0.0.jar"
        project.ext.appPort = 8689
        project.ext.debugPort = 8104
    }

    finalizedBy "deploy"
}

task deployBioAdmin {
    doFirst {
        project.ext.artifactPath = "bio-admin/build/libs"
        project.ext.artifactName = "bio-admin-1.0.0.jar"
        project.ext.appPort = 8590
        project.ext.debugPort = 8205
    }

    finalizedBy "deploy"
}

task deployBioCommandLine {
    doFirst {
        project.ext.artifactPath = "client/bio-commandline/build/libs"
        project.ext.artifactName = "BioStudiesCLI-2.0.jar"
    }

    finalizedBy "deployArtifact"
}

task updatePmcArtifact {
    doFirst {
        project.ext.artifactPath = "scheduler/pmc-processor-task/build/libs"
        project.ext.artifactName = "pmc-processor-task-1.0.0.jar"
    }

    finalizedBy "deployArtifact"
}

task updateReleaserArtifact {
    doFirst {
        project.ext.artifactPath = "scheduler/submission-releaser-task/build/libs"
        project.ext.artifactName = "submission-releaser-task-1.0.0.jar"
    }

    finalizedBy "deployArtifact"
}

task deployArtifact {
    doLast {
        ssh.run {
            session(remotes[env]) {
                put from: "$artifactPath/$artifactName", into: "$deployPath/$artifactName"
            }
        }
    }
}

task deploy {
    doLast {
        ssh.run {
            session(remotes[env]) {
                put from: "$artifactPath/$artifactName", into: "$deployPath/$artifactName"
                put from: "./ci/update.sh", into: "$deployPath/update.sh"

                execute "sed -i -e 's~APP_PATH~$deployPath~g' $deployPath/update.sh"
                execute "sed -i -e 's~APP_PORT~$appPort~g' $deployPath/update.sh"
                execute "sed -i -e 's~APP_NAME~$artifactName~g' $deployPath/update.sh"
                execute "sed -i -e 's~DEBUG_PORT~$debugPort~g' $deployPath/update.sh"

                if (project.hasProperty("jvmParams")) {
                    execute "sed -i -e 's~JVM_PARAMS~$jvmParams~g' $deployPath/update.sh"
                } else {
                    execute "sed -i -e 's~JVM_PARAMS~~g' $deployPath/update.sh"
                }

                execute "chmod +x $deployPath/update.sh"
                execute "sh $deployPath/update.sh"
            }
        }
    }
}
